plugins {
    id 'java'
    id 'com.google.protobuf' version '0.8.10'
    id 'idea'
    id 'jacoco'
}

group 'org.github.felipegutierrez.explore.grpc'
version '1.0'

repositories {
    mavenCentral()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.10.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.24.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

dependencies {
    // implementation 'io.grpc:grpc-netty:1.24.0' // only grpc-netty-shaded includes SSL libraries for gRPC
    implementation 'io.grpc:grpc-netty-shaded:1.24.0'
    implementation 'io.grpc:grpc-protobuf:1.24.0'
    implementation 'io.grpc:grpc-stub:1.24.0'
    implementation "io.grpc:grpc-services:1.24.0"   // reflection

    compile group: 'org.mongodb', name: 'mongodb-driver-sync', version: '4.1.1'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

// if you have source imports issues, add the below
sourceSets.main.java.srcDir new File(buildDir, 'generated/source')
idea {
    module {
        // Marks the already(!) added srcDir as "generated"
        generatedSourceDirs += file('build/generated/source')
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

check.dependsOn jacocoTestReport
